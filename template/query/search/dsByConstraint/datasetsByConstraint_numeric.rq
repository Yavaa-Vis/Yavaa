# get information about all datasets and columns that include the given numeric concept
#
# Variables
# {ds}          ...     SPARQL-fragment to filter datasets
# {concept}     ...     columns concept
# {minValue}    ...     minimum value in range
# {maxValue}    ...     maximum value in range

PREFIX qb:    <http://purl.org/linked-data/cube#>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT ?ds
       ?colConcept
       ?colOrder
       ?role
       ?minValue
       ?maxValue
WHERE {

  # input values
  {ds}
  VALUES ?colConcept { {concept} }

  # get columns
  ?ds qb:structure/qb:component ?col .
  
  # get column properties
  VALUES ?role { qb:dimension qb:measure } .
  ?col qb:order ?colOrder .
  ?col ?role    ?colDesc .
  ?colDesc  rdfs:range  ?colRange .  

  # match values
  ?colDesc  qb:concept/skos:exactMatch                  ?colConcept ;
            rdfs:range/owl:withRestrictions/rdf:first*  ?range .
  ?range    xsd:minInclusive ?minValue ;
            xsd:maxInclusive ?maxValue .
  FILTER( ?minValue < {maxValue} ) .
  FILTER( ?maxValue > {minValue} ) .  
}
