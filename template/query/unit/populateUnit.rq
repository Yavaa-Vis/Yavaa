# Gather describing data about the given units
# includes:
# - label
# - dimension
# - symbol
# - is it a scaled unit?
#
# Variables
# {units}   ... units to populate

PREFIX om: <http://www.wurvoc.org/vocabularies/om-1.8/> 

SELECT  ?unit
        ?label
        ?dimension
        ?symbol
        ?isScaled
WHERE{

# bind input to variable
  VALUES ?unit { {units} } .
  
# get English label for all units
  ?unit rdfs:label ?label .
  FILTER( LANG(?label) = "en" ) .

# the respective dimension
  ?unit om:dimension ?dimension .
  
# the symbol attached
  OPTIONAL {
    ?unit om:symbol ?symbol .
  }
  
# try to find an attached scale
  OPTIONAL {
    ?scale a                  om:Measurement_scale ;
           om:unit_of_measure ?unit .
  }
  BIND( IF( BOUND( ?scale ), true, false ) AS ?isScaled ).
  
}