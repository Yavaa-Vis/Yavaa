# Checks, if there is just one base unit per dimension for all conversions
# grouping by quantity

PREFIX om: <http://www.wurvoc.org/vocabularies/om-1.8/> 
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>

SELECT  ?quant
        (GROUP_CONCAT( DISTINCT ?unit; separator="\n" ) AS ?units)
WHERE {
  
# get the conversion
  ?convBase a om:Unit_of_measure .
  ?convBase om:definition ?conv .
  
# link it to the unit
  ?conv om:unit_of_measure_or_measurement_scale ?unit .
  
# make sure it is a unit
  ?unit a om:Unit_of_measure .
  
# get the respective quantity
  ?quant a om:Quantity .
  ?quant om:unit_of_measure ?unit .
  FILTER NOT EXISTS { 
    ?quant rdfs:subClassOf ?subQuant .
    ?subQuant a om:Quantity .
    FILTER ( ?quant != ?subQuant )
  }
}

GROUP BY ?quant