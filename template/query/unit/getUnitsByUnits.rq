# get similar units for a given unit
# similar means they an be converted to one another
#
# Variables:
# {base}      ...   the source units
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX om: <http://www.wurvoc.org/vocabularies/om-1.8/>

SELECT DISTINCT 
  ?base 
  ?qk 
  ?qkLabel 
  ?unit 
  ?unitLabel
  ?factor
WHERE {
  
  # input
  VALUES ?base { 
    {base}
  }  
  
  # is connected quantity kind
  ?qk a                   om:Quantity ;
      om:unit_of_measure  ?base ;
      om:unit_of_measure  ?unit ;

  # just get new units
  FILTER( ?base != ?unit )

  # for ordering, we need the conversion factor
  {
    {
      ?base om:definition ?conv .
      ?conv  om:unit_of_measure_or_measurement_scale ?unit .
    }
    UNION
    {
      ?unit om:definition ?conv .
      ?conv  om:unit_of_measure_or_measurement_scale ?base .
    }
     
    ?conv om:numerical_value ?factor .
  }
  
  #labels
  ?qk rdfs:label ?qkLabel .
  FILTER( LANG( ?qkLabel ) IN ( "en", "" ) ) .
  ?unit rdfs:label ?unitLabel .
  FILTER( LANG( ?unitLabel ) IN ( "en", "" ) ) .
}